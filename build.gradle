buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'

    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.72"
    id 'org.jetbrains.dokka' version '0.9.17'

    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

allprojects {
    group = 'me.insidezhou.southernquiet'
    version = '4.6.8-SNAPSHOT'

    ext {
        jsonpathVersion = '2.4.0'
        instepVersion = '2.0.6'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task showVersion {
        doLast {
            println 'Version: ' + project.version
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'org.jetbrains.dokka'

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }

    compileJava.dependsOn(processResources)

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        options.encoding = "UTF-8"
    }

    jar {
        enabled = true

        manifest {
            attributes 'Implementation-Version': version
        }
    }

    javadoc {
        options.encoding = 'UTF-8'
        failOnError = false
    }

    bootJar {
        archiveClassifier.set('boot')

        manifest {
            attributes 'Implementation-Version': version
        }
    }

    task sourceJar(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task docJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    publishing {
        publications {
            full(MavenPublication) {
                artifactId jar.baseName

                from components.java

                artifact sourceJar
                artifact docJar

                pom {
                    name = 'SouthernQuiet'
                    description = '这是一个基于Spring Boot，为了提供一些基础设施抽象，减少重复劳动而存在的库。'
                    url = 'https://github.com/InsideZhou/southern-quiet'

                    scm {
                        connection = 'scm:git:git://github.com/InsideZhou/southern-quiet.git'
                        developerConnection = 'scm:git:ssh://github.com/InsideZhou/southern-quiet.git'
                        url = 'https://github.com/InsideZhou/southern-quiet'

                    }

                    licenses {
                        license {
                            name = 'The 3-Clause BSD License'
                            url = 'https://opensource.org/licenses/BSD-3-Clause'
                        }
                    }

                    developers {
                        developer {
                            name = 'Inside Zhou'
                            email = 'inside@insidezhou.me'
                        }
                    }
                }
            }

            simple(MavenPublication) {
                artifactId jar.baseName

                from components.java

                artifact sourceJar
            }
        }

        repositories {
            maven {
                def isSnapshot = version.endsWith("SNAPSHOT")
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'

                url isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
                name 'sonatype'
                credentials {
                    username = project.hasProperty('ossrhUsername') ? ossrhUsername : ''
                    password = project.hasProperty('ossrhPassword') ? ossrhPassword : ''
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.full
    }
}
